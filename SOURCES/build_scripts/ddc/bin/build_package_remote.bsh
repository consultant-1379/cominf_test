#!/bin/tcsh
#------------------------------------------------------------------------
#
#       COPYRIGHT (C) ERICSSON RADIO SYSTEMS AB, Sweden
#
#       The copyright to the document(s) herein is the property of
#       Ericsson Radio Systems AB, Sweden.
#
#       The document(s) may be used and/or copied only with the written
#       permission from Ericsson Radio Systems AB or in accordance with
#       the terms and conditions stipulated in the agreement/contract
#       under which the document(s) have been supplied.
#
#------------------------------------------------------------------------

# History
# 14/10/2013    edavmax         First version


#MAIN

if ( $# != 6 ) then
	echo "Error - incorrect # args"
	exit 1
endif


set BUILD_PACKAGE=$1
set CLEARCASE_VIEW=$2
set CLEARCASE_CSFILE=$3
set VOB_LOCATION=$4
set PACKAGE_FILE=$5
set PACKAGE_VERSION=$6
set PKGIT=$VOB_LOCATION/pkgit
set PACKAGE_NAME=`echo $PACKAGE_FILE | awk -F. '{ print $1 }'` 

#apply config spec
if ( ! -s $CLEARCASE_CSFILE ) then
	echo "Error - failed to locate clearcase config spec file"
	exit 1
endif
/usr/atria/bin/cleartool setview -exec "/usr/atria/bin/cleartool setcs $CLEARCASE_CSFILE" ${CLEARCASE_VIEW}
if ( $? != 0 ) then
	echo "Error - failed to set $CLEARCASE_VIEW config spec"
	exit 1
endif

if ( "$BUILD_PACKAGE" == TRUE ) then
	# delete old package
	/bin/rm -f /var/tmp/${PACKAGE_FILE} >& /dev/null 
	# rebuild package
	set res=`/usr/atria/bin/cleartool setview -exec "/usr/sfw/bin/gmake -f /var/tmp/Makefile.$PACKAGE_NAME" ${CLEARCASE_VIEW}`
	if ( $? != 0 ) then
		echo "Error - failed to build package"
		exit 1
	else
		echo $res | grep "is up to date" > /dev/null
		if ( $? == 0 ) then
			echo "Package was not rebuilt since no code changed"
		else
			echo "Package $PACKAGE_FILE built successfully"
		endif
	endif
	set package_loc=${VOB_LOCATION}/${PACKAGE_FILE}
else
	if ( ! -s $PACKAGE_FILE ) then
		echo "Error - unable to locate $PACKAGE_FILE in clearcase view"
		exit 1
	endif
	set package_loc=$PACKAGE_FILE
endif

# copy package to temp location
/usr/atria/bin/cleartool setview -exec "/bin/cp -f $package_loc  /var/tmp" ${CLEARCASE_VIEW} 
if ( $? != 0 ) then
	echo "Error failed to copy package ${PACKAGE_FILE} from vob"
	exit 1
endif


exit 0
	


